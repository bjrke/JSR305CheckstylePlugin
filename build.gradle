apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

sourceCompatibility = 11
targetCompatibility = 11

group 'de.bjrke.checkstyle'
version '0.5.2-SNAPSHOT'

test {
  useTestNG()
}

configurations {
    checkstylejar
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.puppycrawl.tools:checkstyle:6.17'
    testImplementation 'com.google.code.findbugs:jsr305:3.0.1'
    testImplementation 'com.google.code.findbugs:annotations:3.0.1'
    testImplementation 'org.testng:testng:6.9.10'
}

processResources {
    filesMatching('**/site.xml') {
        expand VERSION: project.version
    }
}

jar {
    version = project.version
    baseName = 'JSR305CheckstylePlugin'
    manifest {
        attributes(
            'Bundle-Name': 'JSR305CheckstylePlugin',
            'Bundle-SymbolicName': 'JSR305CheckstylePlugin;singleton:=true',
            'Bundle-Version': version,
            'Require-Bundle': 'net.sf.eclipsecs.checkstyle, net.sf.eclipsecs.core, net.sf.eclipsecs.ui',
            'Bundle-RequiredExecutionEnvironment': 'J2SE-1.5',
            'Eclipse-LazyStart': 'true',
            'Eclipse-RegisterBuddy': 'net.sf.eclipsecs.core'
        );
    }
}

//known broken versions all < 6.16.1
//versions > 7 need java 8
['6.16.1', '6.17', '6.18', '6.19',
 '7.0', '7.1', '7.1.1', '7.1.2', '7.2','7.3','7.4',
 '7.5', '7.5.1','7.6','7.6.1', '7.7','7.8','7.8.1','7.8.2',
 '8.0', '8.1', '8.2', '8.3', '8.4', '8.5',
 '+' // the latest version
].each { name ->
    def configname = "checkstylejar${name}"
    def testname = "testversion${name}"

    configurations.create(configname);

    configurations[configname].extendsFrom(configurations.testRuntime);
    configurations[configname].resolutionStrategy {
        force "com.puppycrawl.tools:checkstyle:${name}"
    }

    tasks.create(name: testname, type: Test) {
        useTestNG()
        classpath = configurations[configname] + sourceSets.test.runtimeClasspath
        outputs.upToDateWhen { false }
    }

    test.dependsOn(tasks[testname])
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ? 'url_to_snapshot_repository' : 'url_to_release_repository'
            // Supply the credentials using the command line, e.g. "./gradlew publishToMavenLocal -PrepositoryUser='username'"
            credentials {
                username repositoryUser
                password repositoryPassword
            }
        }
    }
}
