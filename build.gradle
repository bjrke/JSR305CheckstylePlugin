apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = '0.5.0';
//version = "0.5.0.SNAPSHOT_${new Date().format("yyyyMMdd-HHmm")}";

test {
  useTestNG()
}

configurations {
    checkstylejar
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.puppycrawl.tools:checkstyle:6.17'
    testCompile 'com.google.code.findbugs:jsr305:3.0.1'
    testCompile 'com.google.code.findbugs:annotations:3.0.1'
    testCompile 'org.testng:testng:6.9.10'
}

processResources {
    filesMatching('**/site.xml') {
        expand VERSION: project.version
    }
}

jar {
    version = project.version
    baseName = 'JSR305CheckstylePlugin'
    manifest {
        attributes(
            'Bundle-Name': 'JSR305CheckstylePlugin',
            'Bundle-SymbolicName': 'JSR305CheckstylePlugin;singleton:=true',
            'Bundle-Version': version,
            'Require-Bundle': 'net.sf.eclipsecs.checkstyle, net.sf.eclipsecs.core, net.sf.eclipsecs.ui',
            'Bundle-RequiredExecutionEnvironment': 'J2SE-1.5',
            'Eclipse-LazyStart': 'true',
            'Eclipse-RegisterBuddy': 'net.sf.eclipsecs.core'
        );
    }
}

//known broken versions all < 6.2, 6.14, 6.16
//versions > 7 need java 8
['6.2', '6.3', '6.4', '6.4.1', '6.5', '6.6', '6.7', '6.8', '6.8.1', '6.8.2', '6.9',
 '6.10', '6.10.1', '6.11', '6.11.1', '6.11.2', '6.12', '6.12.1', '6.13', '6.14.1', '6.15', '6.16.1', '6.17', '6.18', '6.19',
 '7.0', '7.1', '7.1.1', '7.1.2', '7.2','7.3','7.4','7.5','7.5.1','7.6','7.6.1','7.7','7.8','7.8.1','7.8.2'
].each { name ->
    def configname = "checkstylejar${name}"
    def testname = "testversion${name}"

    configurations.create(configname);

    configurations[configname].extendsFrom(configurations.testRuntime);
    configurations[configname].resolutionStrategy {
        force "com.puppycrawl.tools:checkstyle:${name}"
    }

    tasks.create(name: testname, type: Test) {
        useTestNG()
        classpath = configurations[configname] + sourceSets.test.runtimeClasspath
        outputs.upToDateWhen { false }
    }

    test.dependsOn(tasks[testname])
}
